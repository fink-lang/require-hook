{describe, it, mod, mock_fn, expect} = import `@fink/jest`
{to_equal, to_throw, any, to_match_snapshot} = import `@fink/jest`

mock_hooks = []
mock_maps = []

mod.mock `pirates`, fn: dict:
  addHook: mock_fn fn ...args: mock_hooks.push args

mod.mock `source-map-support`, fn: dict:
  install: mock_fn fn ...args: mock_maps.push args


describe `hook`, fn:
  require `.`

  it `registers hook`, fn:
    expect
      mock_hooks
      to_equal
        [[any(Function), {exts: [`.fnk`], ignoreNodeModules: false}]]


  it `transforms fnk`, fn:
    [[hook]] = mock_hooks

    expect
      hook `test = 1234`, `test.fnk`
      to_match_snapshot


  it `throws parse errors`, fn:
    [[hook]] = mock_hooks

    expect
      fn: hook `foo [`, `test.fnk`
      to_throw `test.fnk:1:5
        1| foo [
                ^

        Expected ',' or indented new line or ']'.`


  it `throws transform errors`, fn:
    [[hook]] = mock_hooks

    expect
      fn: hook `123 = foo`, `test.fnk`
      to_throw `test.fnk:1:0
        1| 123 = foo
           ^

        Unable to transform 'assign ='.`


  it `registers source map support`, fn:
    expect
      mock_maps
      to_equal
        list: list: dict:
          handleUncaughtExceptions: false
          environment: `node`
          retrieveSourceMap: any Function


  it `gets source-map`, fn:
    [[{retrieveSourceMap}]] = mock_maps

    expect
      retrieveSourceMap `test.fnk`
      to_match_snapshot


  it `returns null for missing source-map`, fn:
    [[{retrieveSourceMap}]] = mock_maps

    expect
      retrieveSourceMap `test-foo.fnk`
      to_equal null




